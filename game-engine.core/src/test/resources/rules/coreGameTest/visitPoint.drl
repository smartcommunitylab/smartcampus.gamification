package eu.trentorise.game.model

//list any import classes here.
import eu.trentorise.game.core.Utility;

//declare any global variables here
global Utility utils;


rule 'point_interest_reached_go'
salience -2000
no-loop
when
    InputData( $poi : data['poi'], typePoi : data['typePoi'])
	$act : Action(id == 'point_interest_reached')
	$bc : BadgeCollectionConcept(name == typePoi)
    Game( $gameId: id)
    Player( $playerId : id)
    $challenge : ChallengeConcept( modelName == "visitPointInterest", $target: fields["target"] != null, $bonusPointType : fields["bonusPointType"], $bonusScore : fields["bonusScore"], fields["typePoi"] == typePoi, isActive() == true )
then
    utils.log("point_interest_reached_go");

if (! $bc.getBadgeEarned().contains((String) $poi) && !String.valueOf($poi).equals("null")) {
        $bc.getBadgeEarned().add((String) $poi);
        update( $bc );
    }

end


rule 'point_interest_reached_challenge'
lock-on-active true
salience -3000
when
    $challenge : ChallengeConcept( modelName == "visitPointInterest", $target: fields["target"] != null, $bonusPointType : fields["bonusPointType"], $bonusScore : fields["bonusScore"], $bcName : fields["typePoi"], isCompleted() != true )
    $bc : BadgeCollectionConcept(name == $bcName, $badges : badgeEarned, badgeEarned.size() >= (Integer) $target)
    $pc : PointConcept(name == (String) $bonusPointType)
    Player($playerId : id)
    Game( $gameId: id)
then
    utils.log("point_interest_reached_challenge");

    $challenge.completed();
    update($challenge);

	if ($bonusScore instanceof Long) {
	     $pc.setScore($pc.getScore() + (Long)$bonusScore );
	} else if ($bonusScore instanceof Integer) {
	     $pc.setScore($pc.getScore() + (Integer)$bonusScore );
	} else {
	   $pc.setScore($pc.getScore() +  (Double)$bonusScore );
	}

    update($pc);

end